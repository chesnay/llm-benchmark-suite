1 ON BREAK GOSUB 5:GOTO 6
2 GOTO 554:REM SON
3 GOTO 559:REM WAIT
4 GOTO 562:REM DOUBLEH
5 FOR i=0 TO 1000:NEXT:RETURN
6 DEFINT A-Z:DEF FNA(X)=INT(RND(1)*X)+1
7 ENV 1,1,15,1,15,-1,1,10,0,30:ENV 2,1,10,1,5,-2,5,2,0,30:ENV 3,1,15,1,15,-1,4,10,0,10:ENV 4,100,-2,2:ENV 5,100,1,3:ENV 6,1,10,3,1,-10,5
8 ENT 1,100,-2,2:ENT 2,100,5,3
9 AA=FRE(""):X=19:Y=0:R=0:O=6:V=5:AR=0:Z7=1:PX=2:V$="                ":TT$="vous":INK 0,0:INK 1,24:INK 2,19:INK 3,6
10 IF NP=1 THEN GOSUB 298:GOTO 12
11 GOSUB 291
12 GOSUB 91:GOSUB 99:PAPER 0:BORDER 0:CLS:GOSUB 533
13 GOSUB 53:GOSUB 45:BD=0:CT=0:PP=0:Q=0:DS=0:QS=0
14 IF I$="!"THEN 114
15 FOR I=0 TO 55:IF I$=MT$(I)THEN N=NO(I):CM(I)=CM(I)+1:PV=CM(I):GOTO 61
16 NEXT I
17 IF I$="MOTS"AND DM=0 THEN BR=0:GOTO 119
18 IF I$="AIDE"AND DM=0 THEN BR=0:GOTO 108
19 Y$="??????":GOSUB 52:N=0:NL=1:VA=0:RN=RN+1:BR=0
20 IF RN>=3 AND TT=0 THEN RN=0:GOSUB 357
21 IF RN>=2 AND TT=1 THEN RN=0:GOSUB 358
22 IF NQ<5 AND(N>=46 AND N<=55)THEN 439
23 IF N<45 OR N>55 THEN 24 ELSE ON(N-44)GOSUB 122,124,126,128,130,132,133,135,137,138,141
24 IF BD=4 OR PP=4 OR DS=1 THEN 13
25 IF NQ>RG AND CA<(FX/2)THEN 401
26 IF NL=1 THEN 28
27 GOSUB 85
28 IF(N>4 AND N<10)AND VA>6 THEN GOTO 376
29 IF NQ=NT THEN 401
30 IF N>34 AND N<40 THEN GOTO 362
31 IF(N<1 AND N<5)OR N=30 THEN GOSUB 495:GOTO 33
32 IF VA>=10 THEN GOSUB 460
33 IF NL=1 THEN NL=0:GOTO 38
34 GOSUB 55
35 IF Q=7 THEN Q=0:GOSUB 255
36 IF Q=5 THEN Q=0:GOSUB 212
37 IF BR=3 THEN BR=0:GOSUB 392
38 IF(DF-CA>=20 OR CA<(FX/1.4))AND NQ>7 THEN GOSUB 406
39 IF FNA(16)=1 OR NQ=AP THEN GOSUB 411
40 IF FR<2500 THEN GOSUB 333
41 IF FNA(4)=1 AND RI=1 THEN GOSUB 340
42 IF FNA(12)=1 OR(NQ=AP+6)THEN GOSUB 345
43 IF PR=1 THEN GOSUB 389
44 GOTO 13
45 PEN CRAY(3):LOCATE 2,21:PRINT"> "+FF$+SPACE$(35):XX=4:YY=21:I$="":L=LEN(I$):D=48
46 WHILE D<>13:LOCATE XX,YY:PRINT I$+FF$+" ":LOCATE XX+L,YY
47 C$=INKEY$:IF C$=""THEN 47 ELSE C$=UPPER$(C$):D=ASC(C$)
48 IF D=127 THEN IF L>0 THEN L=L-1:I$=LEFT$(I$,L):GOTO 50
49 IF L<25 THEN IF(D>47 AND D<91)OR D=45 OR D=33 OR D=32 THEN L=L+1:I$=I$+C$
50 WEND:LOCATE XX,YY:PRINT I$+"  ":NQ=NQ+1:AR$(AR)=I$:AC(AR)=CA:AR=AR+1:SOUND 135,0:RETURN
51 WA=200:GOSUB 3:NEW
52 CCC=1:YA=7:CA$=Y$:FOR I0=1 TO 3:XA=7:GOSUB 4:WA=30:GOSUB 3:CLS#1:WA=10:GOSUB 3:NEXT I0:RETURN
53 PRINT CHR$(7);
54 CLS#1:RETURN
55 ST=1:IF VA<0 THEN ST=-1
56 FOR I=DF TO CA STEP ST:SOUND 129,2000,-1,0,6:LOCATE#2,34,2:PRINT#2,STR$(I)+"  ";
57 WA=10:GOSUB 3:NEXT I
58 IF CA>=FX THEN AC(AR)=CA:GOTO 403
59 IF CT=0 THEN RETURN
60 LOCATE#2,30,3:PRINT#2,STR$(FR)+"   ":RETURN
61 IF CM(I)>3 THEN GOSUB 441:WA=100:GOSUB 3:GOTO 13
62 IF C=5 OR C=3 THEN 67
63 K=5
64 IF N=K AND CA<(FX/3.3)THEN 439
65 IF N=K+30 AND TT=0 THEN 439
66 K=K+1:IF K<10 THEN 64
67 FOR J=-5 TO 5:IF CR(C,N)=J THEN VA=J*3:IF CA>(FX/2.6)THEN VA=J*5
68 NEXT J
69 IF PV>1 THEN VA=VA-PV
70 Y$="!!!!!!":GOSUB 52
71 IF RI=1 AND FNA(9)=1 THEN GOSUB 520:BR=0:GOTO 13
72 GOSUB 73:GOTO 22
73 DF=CA:CA=CA+VA
74 IF DF=CA OR CA<DF THEN RN=RN+1
75 IF DF>CA THEN RN=0
76 IF CA-DF>=9 THEN BR=BR+1 ELSE BR=0
77 RETURN
78 VA=30:IF CA>(FX/2.6)THEN VA=40
79 IF Q=4 THEN VA=INT(VA/1.5)
80 RETURN
81 SOUND 7,1012,-1,0,3:WA=400:GOSUB 3:RETURN
82 VA=-(5*Y):RETURN
83 VA=-50:RETURN
84 LOCATE#2,30,3:PRINT#2,"     ":RETURN
85 IF CA<DF AND DF-CA>30 THEN GOSUB 326:GOTO 272
86 IF CA<30 OR CA<DF THEN GOSUB 278:GOSUB 115:RETURN
87 U=1:FOR H=30 TO FX STEP SP:IF CA>=H AND CA<=H+(SP-1)THEN JV=U
88 U=U+1:NEXT H
89 ON JV GOSUB 263,264,279,268,282,280,281,281,281
90 RETURN
91 BORDER 0:PAPER 3:CLS:WINDOW#1,1,40,7,19:PAPER#1,0:CLS#1:INK 2,14:PEN#1,2:CCC=1:XA=5:YA=4:CA$="NIVEAU DE DIFFICULTE 1,2 OU 3 ?":GOSUB 4
92 INK 1,24:PEN#1,1:LOCATE#1,2,11:PRINT#1,"Niveau 3: ni aide ni liste possible.":GOSUB 107
93 J$=INKEY$:IF J$=""THEN 93 ELSE J$=UPPER$(J$):NN=ASC(J$)-48
94 IF NN<1 OR NN>3 THEN GOTO 93
95 IF J$="1"THEN FX=500:RG=29:DM=0:FR=9000:SP=65:GL=4000:NT=48
96 IF J$="2"THEN FX=700:RG=27:DM=0:FR=8000:SP=75:GL=3500:NT=52
97 IF J$="3"THEN FX=800:RG=34:DM=1:FR=8000:SP=100:GL=3000:NT=60
98 RETURN
99 WINDOW#1,1,40,3,23:CLS#1:PEN#1,2:CCC=1:XA=6:YA=3:CA$="COMMENT LA DESIREZ -VOUS":GOSUB 4
100 INK 1,19:PEN#1,1:LOCATE#1,1,8:PRINT#1,"      1- Rousse flamboyante?":PRINT#1:PRINT#1,"      2- Brune mysterieuse?":PRINT#1
101 PRINT#1,"      3- Brune nocturne?":PRINT#1:PRINT#1,"      4- Brune volcanique?":PRINT#1
102 PRINT#1,"      5- Blonde incandescente?":LOCATE#1,9,19:PRINT#1,"Un chiffre,svp":GOSUB 107
103 CC$=INKEY$:IF CC$=""THEN 103 ELSE CC$=UPPER$(CC$):F=ASC(CC$)-48
104 IF F<1 OR F>6 THEN 103:SAVE"don2.txt",A
105 C=FNA(10)-1:CP=FNA(3)+2:AP=FNA(4)+7
106 RETURN
107 SOUND 7,1073,-1,0,3:RETURN
108 IF DJ=1 THEN 19
109 FOR I=0 TO 9:IF N(I)=C THEN CT=2000:DJ=1:NC$=N$(I)
110 NEXT I
111 R$(1)="HUM...":R$(2)="POUR MA PART,":R$(3)="JE LA CROIS ..":R$(4)="UN PEU...":R$(5)=NC$+".":AJ=CT:GOSUB 161
112 GOSUB 60:PRINT CHR$(7);:GOSUB 456:SOUND 7,1012,-5,0,3
113 WA=400:GOSUB 3:GOTO 13
114 INK 2,14:INK 1,26:PAPER 2:CLS:BORDER 14:CCC=0:XA=11:YA=11:CA$="ADIEU  VILAINE":PEN 1:GOSUB 4:GOSUB 339:GOTO 285
115 IF DF-CA<20 THEN RETURN
116 IJ=FNA(2):R$(1)="...POUAH!!!":R$(2)="...BEUHHH!":XJ=PX+2:SOUND 7,800,-5,0,3:WA=40:GOSUB 3
117 RETURN
118 RETURN
119 IF FR<300 THEN 450
120 LOCATE XX-2,YY:PRINT V$:FOR I=0 TO 55:LOCATE XX,YY:PRINT MT$(I):PRINT CHR$(7):WA=60:GOSUB 3:LOCATE XX,YY:PRINT V$:NEXT I
121 FR=FR-300:GOSUB 60:GOTO 13
122 M$(1)="SUJET DE CONVERS?   ":M$(2)="1-Travail/argent.":M$(3)="2-Amour..........":M$(4)="3-Cinema/vedettes":M$(5)="4-Philosophie....":M$(6)="5-Politique......":M$(7)=V$:M$(8)=V$
123 Q=4:CT=0:GOTO 143
124 M$(1)="    OU L'INVITER ?  ":M$(2)="1-Maxim's.........":M$(3)="2-L'auberge.......":M$(4)="    fleurie.......":M$(5)="3-La guinguette...":M$(6)="   a Bebert.......":M$(7)="4-Hamburger-Quick.":M$(8)="5-Sandwich........"
125 CT=2000:Q=7:GOTO 143
126 M$(1)="   OU L'EMMENER ? ":M$(2)="1-Tahiti.....8000f":M$(3)="2-Croisiere..4000f":M$(4)="3-Cannes.....2000f":M$(5)="4-L'auvergne......":M$(6)="  Sauvage ...1000f":M$(7)="5-Stage/voile.....":M$(8)=" -gratuit-........"
127 ct=8000:Q=5:GOTO 143
128 M$(1)="   QUE DANSER ?   ":M$(2)="1-Valse...........":M$(3)="2-SLOW langoureux.":M$(4)="3-Rock N' Roll....":M$(5)="4-Tango...........":M$(6)=V$:M$(7)=V$:M$(8)=V$
129 Q=9:GOTO 143
130 M$(1)="    OU ALLER ?     ":M$(2)="1-Shopping........":M$(3)="2-Jardin du.......":M$(4)="     Luxembourg...":M$(5)="3-Restaurant......":M$(6)="4-Musee du Louvre.":M$(7)="5-Night club......":M$(8)=V$
131 Q=1:CT=0:GOTO 143
132 GOTO 122
133 M$(1)=" QUE LUI OFFRIR ?  ":M$(2)="1-Diamant...7000f":M$(3)="2-Or fin....3500f":M$(4)="3-Montre....1750f":M$(5)="4-Babiole....100f":M$(6)=V$:M$(7)=V$:M$(8)=V$
134 Q=2:CT=7000:GOTO 143
135 M$(1)="  QUE LUI OFFRIR ? ":M$(2)="1-Bijou...........":M$(3)="2-Fleurs..........":M$(4)="3-Livre de cuisine":M$(5)="4-Disque de musi-":M$(6)="  que classique...":M$(7)="5-Disque Rock.....":M$(8)=V$
136 Q=3:CT=150:GOTO 143
137 GOTO 139
138 GOTO 130
139 M$(1)="  QUE LUI OFFRIR ? ":M$(2)="1-Tailleur Channel":M$(3)="2-Robe dentelle...":M$(4)="3-Mini jupe.......":M$(5)="4-Pantalon'Jeans..":M$(6)=V$:M$(7)=V$:M$(8)=V$
140 CT=3000:Q=6:GOTO 143
141 M$(1)="  QUEL WEEK-END ?  ":M$(2)="1-Avec des amis..":M$(3)="2-Seuls,chez vous":M$(4)="3-A la campagne..":M$(5)="4-A Londres......":M$(6)="5-W/E sportif....":M$(7)=V$:M$(8)=V$
142 Q=8
143 LOCATE#1,1,3:FOR I=1 TO 8:PRINT#1,M$(I):NEXT I
144 LOCATE#1,3,13:PRINT#1,"Un chiffre,svp-":LOCATE XX,YY:PRINT V$
145 A$=INKEY$:IF A$=""THEN 145 ELSE A$=UPPER$(A$):Y=ASC(A$)-48:IF Y<1 OR Y>5 THEN 145 ELSE CLS#1
146 IF Q=9 THEN 242
147 IF(PV>1 AND CA>(FX/1.5)AND FR>40)THEN IF FNA(4)=1 THEN BR=0:GOTO 387
148 IF Q=8 THEN GOSUB 219:GOSUB 73:AJ=CT:GOTO 160
149 IF Q=1 AND Y=1 THEN 139
150 IF Q=1 AND Y=3 THEN 124
151 IF Q=2 AND Y=4 THEN CT=800
152 IF Q=1 AND Y=5 THEN CT=32000
153 IF Q=3 AND Y=1 THEN 133
154 IF Q=5 AND Y=5 THEN CT=0
155 IF FNA(14)=1 AND CA<(FX/1.6)THEN GOSUB 336:BD=4:BR=0:RETURN
156 ON Y GOSUB 169,174,185,194,204
157 GOSUB 73
158 FOR I=1 TO 5:IF Y=I THEN AJ=CT
159 CT=CT/2:NEXT I
160 IF AJ>FR THEN AJ=0:GOTO 450
161 FR=INT(FR-AJ):IF FR<=0 THEN FR=0:GOSUB 449:GOTO 24
162 RETURN
163 IF(C=2 OR C=1)AND(Q=1 OR Q=7 OR Q=6)THEN GOSUB 78:RETURN
164 IF(C=8 AND Q=1)OR(C=0 AND Q=7)THEN GOSUB 78:RETURN
165 GOSUB 82:RETURN
166 IF C=1 AND Q=6 THEN GOSUB 83:RETURN
167 IF C=1 AND Q=7 THEN GOSUB 78:RETURN
168 GOSUB 82 RETURN
169 IF(C=8 OR C=4 OR C=10)AND Q<8 THEN GOSUB 78:RETURN
170 IF(C=6 OR C=5)AND Q=7 THEN GOSUB 83:RETURN
171 IF(C=5 OR C=7)AND Q=2 THEN GOSUB 83:RETURN
172 IF C=9 AND Q=4 THEN GOSUB 78:RETURN
173 GOSUB 82:RETURN
174 IF C=3 AND(Q=4 OR Q=5 OR Q=7)THEN GOSUB 78:RETURN
175 IF(C=10 OR C=9)AND Q=4 THEN GOSUB 78:RETURN
176 IF(C=0 OR C=1)AND Q<7 THEN GOSUB 78:RETURN
177 IF(C=8 OR C=2 OR C=1)AND(Q=3 OR Q=2)THEN GOSUB 78:RETURN
178 IF(C=6 OR C=5 OR C=7 OR C=9)AND Q=6 THEN GOSUB 83:RETURN
179 IF C=5 AND Q=5 THEN GOSUB 83:RETURN
180 IF(C=8 OR C=6)AND Q=4 THEN GOSUB 83:RETURN
181 IF C=2 OR C=8 OR C=1 OR C=0 THEN 163
182 IF C=9 AND Q=7 THEN GOSUB 83:RETURN
183 IF C=9 AND(Q=2 OR Q=3)THEN GOSUB 83:RETURN
184 GOSUB 82:RETURN
185 IF(C=2 AND Q<>7)OR(C=3 AND Q=6)THEN GOSUB 78:RETURN
186 IF(C=1 OR C=3 OR C=5)AND Q=4 THEN GOSUB 78:RETURN
187 IF(C=10 OR C=9)AND(Q=7 OR Q=1)THEN GOSUB 78:RETURN
188 IF(C=0 OR C=3 OR C=4 OR C=6 OR C=7)AND Q=3 THEN GOSUB 83:RETURN
189 IF(C=6 OR C=7)AND Q=4 THEN GOSUB 83:RETURN
190 IF(C=6 OR C=9)AND Q=5 THEN GOSUB 83:RETURN
191 IF C=7 AND Q=7 THEN GOSUB 83:RETURN
192 IF C=1 THEN 166
193 GOSUB 82:RETURN
194 IF C=6 OR C=7 OR C=10 THEN GOSUB 78:RETURN
195 IF(C=9 OR C=5)AND(Q=5 OR Q=6 OR Q=7)THEN GOSUB 78:RETURN
196 IF C=5 AND(Q=2 OR Q=5 OR Q=6)THEN GOSUB 78:RETURN
197 IF(C=3 OR C=4 OR C=9)AND Q=1 THEN GOSUB 83:RETURN
198 IF(C=0 OR C=8 OR C=2)AND Q=6 THEN GOSUB 83:RETURN
199 IF(C=0 OR C=2 OR C=4 OR C=8)AND Q=7 THEN GOSUB 83:RETURN
200 IF(C=4 OR C=8)AND Q=2 THEN GOSUB 83:RETURN
201 IF C=2 AND Q=5 THEN GOSUB 83:RETURN
202 IF C=8 AND(Q=3 OR Q=1 OR Q=8)THEN GOSUB 78:RETURN
203 GOSUB 82:RETURN
204 IF C=9 OR C=5 THEN GOSUB 78:RETURN
205 IF(C=3 OR C=4 OR C=5)AND Q=1 THEN GOSUB 78:RETURN
206 IF(C=10 OR C=7 OR C=6)AND(Q=4 OR Q=7)THEN GOSUB 78:RETURN
207 IF(C=3 OR C=0 OR C=1 OR C=2 OR C=4 OR C=7 OR C=8)AND Q=5 THEN GOSUB 83:RETURN
208 IF(C=0 OR C=1 OR C=2)AND Q=4 THEN GOSUB 83:RETURN
209 IF(C=1 OR C=2 OR C=8)AND Q=3 THEN GOSUB 83:RETURN
210 IF(C=3 OR C=9)AND Q=3 THEN GOSUB 78:RETURN
211 GOSUB 82:RETURN
212 IF FNA(2)=1 THEN 216
213 R$(1)="Triste voyage!":R$(2)="La region etait":R$(3)="infestee de ":R$(4)="moustiques."
214 IF FNA(2)=1 THEN R$(2)="Il a plu":R$(3)="24 H SUR 24!":R$(4)=V$
215 R$(5)=V$::VA=-15:GOSUB 262:GOTO 218
216 R$(1)="Temps splendide!":R$(2)="Ce voyage":R$(3)="fut":VA=15:R$(4)="une reussite.":IF CA<DF THEN R$(3)="fut quand meme"
217 R$(5)=V$:GOSUB 269
218 CLS#1:PRINT CHR$(7);:GOSUB 456:WA=200:GOSUB 3:GOSUB 73:GOSUB 55:RETURN
219 IF Y=1 OR Y=5 THEN CT=500
220 IF Y=2 THEN CT=50
221 IF Y=3 THEN CT=750
222 IF Y=4 THEN CT=1250
223 ON Y GOSUB 224,227,232,235,237:RETURN
224 IF(C=6 OR C=7 OR C=9)AND CA<(FX-100)THEN VA=25:RETURN
225 IF(C=3 OR C=4 OR C=5)AND CA>100 THEN VA=-50:RETURN
226 VA=0:RETURN
227 IF(C=5 OR C=3 OR C=4)AND CA>150 THEN VA=50:RETURN
228 IF(C=8 OR C=9 OR C=6)AND CA<(FX-60)THEN VA=-80:RETURN
229 IF CA>=(FX/1.4)THEN VA=30:RETURN
230 IF CA<(FX/1.6)THEN VA=-50:RETURN
231 VA=-10:RETURN
232 IF C=0 OR C=1 OR C=9 THEN VA=20:RETURN
233 IF C=2 OR C=6 THEN RETURN
234 VA=-10:RETURN
235 IF C=10 OR C=8 OR C=4 OR C=6 THEN VA=50:RETURN
236 VA=0:RETURN
237 IF C=9 OR C=5 THEN VA=50:RETURN
238 IF C=8 OR C=0 OR C=7 OR C=10 THEN VA=-60:RETURN
239 VA=-20:RETURN
240 O=FNA(2):IF O>=2 THEN GOSUB 78:RETURN
241 GOSUB 82:RETURN
242 IF CA<(FX/3)THEN RETURN
243 IF Y=2 AND C=3 THEN VA=30:GOTO 250
244 IF Y=2 AND TT=1 THEN VA=15:GOTO 250
245 IF Y=2 AND CA>(FX/1.2)THEN VA=30:GOTO 250
246 IF Y=1 AND(C=0 OR C=8)AND CA<(FX/1.3)THEN VA=10:GOTO 250
247 IF C=6 AND TT=0 THEN VA=-20:GOTO 250
248 IF Y=3 AND(C=9 OR C=5)THEN VA=10:GOTO 250
249 VA=5
250 GOSUB 73
251 IF FNA(3)=1 THEN 253
252 RETURN
253 GOSUB 54:R$(1)="Maladroit!":R$(2)="En dansant,":R$(3)="vous lui avez":R$(4)="marche sur":R$(5)="les pieds.":GOSUB 55:WA=70:GOSUB 3
254 VA=-5:GOSUB 262:GOSUB 456:GOSUB 339:WA=180:GOSUB 3:GOSUB 73:GOSUB 55:DS=1:RETURN
255 RA=FNA(3):IF RA>1 THEN 258
256 R$(1)="Facheux:durant":R$(2)="le repas,vous":R$(3)="avez renverse":R$(4)="le ketchup":R$(5)="sur sa robe.":VA=-10
257 GOSUB 262:CLS#1:GOSUB 107:GOSUB 456:WA=300:GOSUB 3:GOSUB 73:GOSUB 55:RETURN
258 IF RA=2 THEN 261
259 R$(1)="Vous avez bien ":R$(2)="choisi les vins:":R$(3)="elle est":R$(4)=" pompette.":R$(5)=V$:VA=10
260 GOSUB 268:GOSUB 262:PRINT CHR$(7);:GOSUB 456:WA=220:GOSUB 3:GOSUB 73:GOSUB 55:RETURN
261 R$(1)="Durant le repas,":R$(2)="une jolie fille":R$(3)="n'a pas cesse de":R$(4)="vous regarder:":R$(5)="votre cote monte.":VA=20:GOTO 257
262 PEN#4,0:LOCATE#4,2,2:PRINT#4,OI0$:LOCATE#4,2,3:PRINT#4,OI2$+"  "+OI2$:LOCATE#4,3,4:PRINT#4,OI3$;"    ";OI3$:YE=0:RETURN
263 GOSUB 262:WA=50:GOSUB 3:SOUND 7,319,10,0,4:GOSUB 269:WA=100:GOSUB 3:SOUND 239,10,0,4:GOSUB 262:RETURN
264 FOR I=1 TO 2:GOSUB 263:NEXT I:GOSUB 262:RETURN
265 GOSUB 269:GOSUB 270:PEN#4,0:LOCATE#4,2,3:PRINT#4,OI2$:LOCATE#4,3,4:PRINT#4,OI3$:PRINT CHR$(7);
266 WA=60:GOSUB 3
267 GOSUB 269:WA=10:GOSUB 3:RETURN
268 GOSUB 263:GOSUB 270:SOUND 1,600,-1,0,6:RETURN
269 PEN#4,0:LOCATE#4,2,3:PRINT#4,OI1$+"  "+OI1$:LOCATE#4,3,4:PRINT#4,CHR$(244);"    ";CHR$(244):YE=1:RETURN
270 PEN#4,CRAY(7):LOCATE#4,4,7:PRINT#4,B2$:SOUND 129,500,-1,0,6:WA=80:GOSUB 3:LOCATE#4,4,7:PRINT#4,B3$:SOUND 129,1000,-1,0,6:GOSUB 269:RETURN
271 PEN#4,0:FOR I=1 TO 8:LOCATE#4,4,7:PRINT#4,"    ":LOCATE#4,5,8:PRINT#4,"  ":WA=10:GOSUB 3:GOSUB 275:WA=25:GOSUB 3:NEXT I:SOUND 129,200,-1,0,6:GOSUB 277:WA=30:GOSUB 3:GOSUB 275:GOSUB 560:RETURN
272 PEN#4,0:B6$=CHR$(246):B7$=CHR$(250):LOCATE#4,2,2:PRINT#4,B6$+B6$+B6$+B6$+B7$+B7$+B7$+B7$:GOSUB 269:GOSUB 277
273 FOR I1=1 TO 10:LOCATE#4,2,3:PRINT#4,"        ":SOUND 135,800,55,0,1,1:WA=8:GOSUB 3:GOSUB 269:WA=10:GOSUB 3:NEXT I1:PRINT CHR$(7);:WA=200:GOSUB 3:GOSUB 262:RETURN
274 WA=200:GOSUB 262:RETURN
275 PEN#4,CRAY(7):LOCATE#4,4,7:PRINT#4,B3$:LOCATE#4,5,8:PRINT#4,B1$:RETURN
276 PEN#4,CRAY(7):LOCATE#4,4,7:PRINT#4,B3$:LOCATE#4,5,8:PRINT#4,B1$:RETURN
277 PEN#4,CRAY(7):LOCATE#4,4,7:PRINT#4,B5$:RETURN
278 GOSUB 262:GOSUB 275:RETURN
279 GOSUB 264:GOSUB 269:RETURN
280 GOSUB 265:GOSUB 271:GOSUB 270:RETURN
281 GOSUB 265:GOSUB 270:GOSUB 271:GOSUB 270:RETURN
282 GOSUB 263:GOSUB 265:GOSUB 270:RETURN
283 LOCATE#1,2,5:RETURN
284 LOCATE 16,25:PRINT"POUR CONTINUER 'ENTER' ":C$=INKEY$:IF C$=""THEN 284 ELSE C$=UPPER$(C$):RETURN
285 INK 1,8:PAPER 1:BORDER 8:INK 2,1:PEN 2:CLS:PRINT CHR$(7):LOCATE 6,10:PRINT"UNE NOUVELLE PARTIE -O / N- ?"
286 N$=INKEY$:IF N$=""THEN 286 ELSE N$=UPPER$(N$):IF N$="N"THEN LOCATE 12,13:PRINT"AU REVOIR !":GOTO 51
287 CLS:LOCATE 8,11:PRINT"QUELQUES SECONDES ,svp.":NL=0:DM=0:RN=0:NQ=0:TT=0:BR=0:Z1=0:Z2=0:Z3=0:Z8=0:z9=0:IN=0:K=0:RI=0:BD=0
288 FR=0:DJ=0:CA=0:CF=0:NP=NP+1:FOR I=0 TO 55:CM(I)=0:NEXT I:FOR I=0 TO 65
290 AR$(I)="0":AC(I)=0:NEXT I:FOR I=0 TO 11:P(I)=0:NEXT I:FOR I=0 TO 9:AN(I)=0:NEXT I:RESTORE:GOTO 9
291 PAPER 3:BORDER 6:PEN 0:CLS:FOR I=3 TO 22:LOCATE 3,I:PRINT CHR$(207):LOCATE 36,I:PRINT CHR$(207):NEXT I:FOR I=3 TO 36:LOCATE I,2:PRINT CHR$(207):LOCATE I,23:PRINT CHR$(207):NEXT I
292 WINDOW#1,5,34,4,21:PRINT CHR$(7):PAPER#1,0:CLS#1:PEN#1,2:CA$="*** DON JUAN ***":CCC=1:XA=8:YA=4:GOSUB 4:WA=80:GOSUB 3
293 INK 1,6:PEN#1,1:GOSUB 383:WA=80:GOSUB 3
294 AF$="JEU DE STRATEGIE":FX=8:FY=15:CCC=1:PEN#1,2:GOSUB 398
295 AF$="-amoureuse-":FX=11:FY=17:CCC=1:GOSUB 398
296 AF$=" "+CHR$(164)+" 1984. DUC. ":FX=13:FY=23:CCC=0:GOSUB 398:WA=20:GOSUB 3:INK 1,6,0:PRINT CHR$(7)
297 PEN#1,1:GOSUB 384
298 INK 2,14:INK 1,24:PAPER 3:PEN 2:BORDER 14:CLS:LOCATE 2,11:PRINT"VOULEZ-VOUS LA REGLE DU JEU - O/N -?":PRINT CHR$(7);
299 A$=INKEY$:IF A$=""THEN GOTO 299 ELSE A$=UPPER$(A$)
300 IF A$="N"THEN RETURN
301 CLS:PEN 0:BORDER 6:GOSUB 554:LOCATE 1,2:PRINT" Elles sont des dizaines a habiter":PRINT" votre ordinateur, oui. Mais il faut"
302 PRINT" maintenant les seduire. ":PRINT" L'animation progressive de leur visa-":PRINT" ge -sourire, clin d'oeil, baiser,etc-":PRINT" et l'affichage de votre COTE D'AMOUR"
303 PRINT" vous diront ou vous en etes!":PRINT:PRINT" Mais attention: un rival PEUT surve-":PRINT" nir et vous faucher la belle.":PRINT
304 PEN 1:PRINT" Pour faire monter votre cote d'amour":PRINT" il suffit d'entrer UN a UN les MOTS-":PRINT" CLEFS qui les seduiront,en essayant":PRINT" de cerner leur personnalite.Chaque":PRINT" mot represente a lui seul une IDEE,"
305 PRINT" vous permettant au choix:":PRINT:PEN 0:PRINT"    -de leur parler de vous.":PRINT"    -de leur faire des compliments.":PRINT"    -de leur faire des propositions.":LOCATE 25,24:PRINT"'ENTER'"
306 IF INKEY$=""THEN GOTO 306
307 CLS:PRINT" MOTS-CLES: Entrez les verbes a l'in-":PRINT" finitif, les noms au masculin. Pensez":PRINT" aux synonymes...":PRINT:PRINT"    !!!!!! = Elle a compris le mot.":PRINT"    ?????? = Elle n'a pas compris.":PRINT
308 PEN 1:PRINT" ATTENTION:vous ne pouvez entrer plus":PRINT" de 3 fois le meme mot, s'il se reve-":PRINT" le efficace, ni acceder plus de 3 ":PRINT" fois au 'menus' qui vous seront pro-"
309 PRINT" poses.Mais un mot d'abord ineffica-":PRINT" ce peut se reveler bon plus tard.":PRINT
310 PEN 0:PRINT" Une seule fois vous pourrez demander":PRINT" de l'aide -entrez 'AIDE' ,...au prix":PRINT" de 2000 frcs d'honoraires/ordinateur.":PRINT:PRINT" LISTE MOTS CLEFS:Entrez ' MOTS '.":PRINT" Honoraires: 300 Frcs a chaque fois."
311 PRINT:PEN 1:PRINT" PARTIE GAGNEE:Il faut atteindre une":PRINT" COTE D'AMOUR de 500 -niveau 1-, 700":PRINT" -niv.2- ou 800 -niv.3-.":LOCATE 28,25:PRINT"'ENTER'"
312 IF INKEY$=""THEN GOTO 312
313 RETURN
314 INK 1,18:INK 2,6:INK 3,2:PAPER 3:CLS:PEN 2:CCC=0:XA=8:YA=10:CA$=" PARTIE  "+PG$:GOSUB 4:WA=150:GOSUB 3:PRINT CHR$(7);:PAPER 0:BORDER 0:CLS:PEN 2:PRINT CHR$(22)+CHR$(1):PRINT STRING$(&27,FF$):PEN 0:LOCATE 2,2
315 PRINT"La belle etait en effet...un rien":PRINT:LU=LEN(N$(C))
316 PEN 3:PRINT STRING$(&27,FF$);:PEN 2:LOCATE 14,4:PRINT N$(C)+"...":PRINT CHR$(22)+CHR$(0):WA=150:GOSUB 3:PRINT:PEN 1:PRINT"  Pour la seduire, vous avez utilise":PRINT"  les mots que voici,suivis de votre":PRINT"  cote d'amour:":PRINT:WA=300:GOSUB 3
317 FOR I=0 TO AR-1:LG=LEN(AR$(I))
318 IF LG>13 THEN AR$(I)=LEFT$(AR$(I),13):LG=13
319 PRINT"        -"+AR$(I);:NP=14-LG:WHILE PN<>NP:PRINT".";:PN=PN+1:WEND
320 PRINT AC(I+1):SOUND 1,2000,-1,0,6:PN=0:WA=60:GOSUB 3:NEXT I:PRINT:PEN 2:PRINT STRING$(&27,FF$);:LOCATE 12,25:PRINT CHR$(22)+CHR$(1);:PEN 0:PRINT CM$:PRINT CHR$(22)+CHR$(0);:PEN 2
321 IF LEFT$(CM$,1)="H"THEN PRINT:PRINT"...UNE DE PERDUE, DIX DE RETROUVEES!"
322 PRINT:PRINT SPACE$(25)+"'ENTER'";
323 RT$=INKEY$:IF RT$=""THEN 323
324 GOTO 285
325 GOSUB 339:CLS:PRINT CHR$(7)
326 CCC=1:YA=3:IF TT=1 THEN CA$=PR$+",":GOSUB 402:CA$="TU ME DESOLES!":GOSUB 402:GOTO 332
327 CA$="GOUJAT!!!":R$(3)=""
328 IF IN=1 THEN CA$="MALOTRU!"
329 IF IN=2 THEN CA$="ABRUTI!"
330 IF IN=3 THEN CA$="FRIPON!"
331 GOSUB 402:CA$=R$(3):GOSUB 402:IN=IN+1
332 RETURN
333 IF FNA(8)<>8 THEN RETURN
334 R$(1)="..Chance:":R$(2)="vous avez gagne":R$(3)="au loto.":LO=FNA(GL)+2000:IF C=4 OR C=8 OR C=10 THEN LO=5999
335 FR=FR+LO:R$(4)="Gain:"+STR$(LO)+"Frcs":R$(5)=V$:PRINT CHR$(7);:GOSUB 456:WA=300:GOSUB 3:GOSUB 60:RETURN
336 GOSUB 277:GOSUB 262
337 R$(1)="Allons bon!":R$(2)="ELLE BOUDE.":R$(3)="...Coup nul.":R$(4)=V$:R$(5)=V$
338 GOSUB 456:GOSUB 339:WA=40:GOSUB 3:RETURN
339 SOUND 1,284,200,15,0,1:WA=550:GOSUB 3:PRINT CHR$(7);:RETURN
340 REM
341 R$(1)="BONNE":R$(2)="NOUVELLE":R$(3)="votre rival":R$(4)="a fait ":R$(5)="une gaffe."
342 IF FNA(2)=1 THEN R$(4)="n'a pas":R$(5)="le moral."
343 IF FNA(2)=1 THEN R$(4)="est souffrant.":R$(5)=V$
344 GOSUB 456:GOSUB 107:WA=200:GOSUB 3:VA=10:GOSUB 73:GOSUB 55:GF=GF+1:RETURN
345 IF NQ<9 THEN RETURN
346 R$(1)="QU'EST-CE?..":GOSUB 54:IJ=1:XJ=PX+2:GOSUB 277
347 GOSUB 262:GOSUB 117:GOSUB 339:BT$=CHR$(253):PEN#4,0:GOSUB 354:PRINT CHR$(7):GOSUB 54
348 R$(1)="FACHEUSE POUSSEE":R$(2)="DE BOUTONS:":R$(3)="elle refuse ":R$(4)="de vous voir.":R$(5)="...Coup nul.":LOCATE#1,XX,YY:PRINT#1,V$
349 GOSUB 456:GOSUB 339:WA=80:GOSUB 3
350 IF FNA(2)=1 THEN BT$=" ":PEN#4,CRAY(5):GOSUB 353:BD=4:BR=0:NQ=NQ+1:RETURN
351 R$(1)="Chance:":R$(2)="vous connaissez":R$(3)="un bon remede":R$(4)="contre l'acne.":R$(5)=V$:CLS#1:PRINT CHR$(7):GOSUB 456:WA=200:GOSUB 3:
352 BT$=" ":PEN#4,CRAY(5):GOSUB 354:GOSUB 270:VA=30:GOSUB 73:GOSUB 55:RETURN
353 R$(1)="Huit jours":R$(2)="plus tard...":R$(3)=V$:R$(4)=V$:R$(5)=V$:CLS#1:GOSUB 456:WA=80:GOSUB 3
354 LOCATE#4,2,5:PRINT#4,BT$:SOUND 1,409,-1,0,6:WA=60:GOSUB 3:LOCATE#4,8,5:SOUND 1,1000,-1,0,6:PRINT#4,BT$:WA=60:GOSUB 3:LOCATE#4,5,4:SOUND 1,2000,-1,0,6:PRINT#4,BT$:WA=70:GOSUB 3
355 LOCATE#4,6,2:SOUND 1,1000,-1,0,6:PRINT#4,BT$:WA=60:GOSUB 3:LOCATE#4,9,6:SOUND 1,1000,-1,0,6:PRINT#4,BT$:WA=70:GOSUB 3:LOCATE#4,3,5:SOUND 1,1000,-1,0,6:PRINT#4,BT$:WA=50:GOSUB 3
356 LOCATE#4,3,6:SOUND 1,1000,-1,0,6:PRINT#4,BT$:WA=70:GOSUB 3:LOCATE#4,10,5:SOUND 1,1000,-1,0,6:PRINT#4,BT$;:WA=60:GOSUB 3:LOCATE#4,9,4:SOUND 1,1000,-1,0,6:PRINT#4,BT$;:WA=60:GOSUB 3:RETURN
357 R$(1)="Pauvre diable !":R$(2)="...Il cafouille.":R$(3)="Il lui faut un":R$(4)="encouragement.":R$(5)=V$:GOTO 359
358 R$(1)="EH BIEN,":R$(2)=PR$+",":R$(3)="JE NE TE PLAIS":R$(4)="PLUS? TU":R$(5)="CAFOUILLES."
359 CLS#1:GOSUB 451:SOUND 129,500,-1,0,6:WA=100:GOSUB 3:SOUND 129,900,-1,0,6
360 IF YE=0 THEN GOSUB 269:WA=170:GOSUB 3:GOSUB 262:RETURN
361 IF YE=1 THEN GOSUB 265:RETURN
362 IF C=3 OR C=5 OR C=4 OR C=10 THEN 371
363 IF Z9=1 THEN 368
364 IF Z9=2 THEN 376
365 R$(1)="Petit coquin:":R$(2)="je lis dans":R$(3)="vos pensees!"
366 IF TT=1 THEN R$(3)="tes pensees!"
367 R$(4)=V$:R$(5)=V$:Z9=1:GOTO 375
368 R$(1)="...Vous alors!":R$(2)="quand vous":R$(3)="avez une idee":R$(4)="en tete!":R$(5)=V$
369 IF TT=1 THEN R$(1)="Toi alors!":R$(2)="Quand tu as"
370 Z9=2:GOTO 375
371 IF Z9=1 THEN 376
372 R$(1)="Je coure,":R$(2)="je vole,":R$(3)="j'y suis deja!":R$(4)=V$:R$(5)=V$:Z9=1:GOTO 375
373 IF Z9=1 THEN 376
374 R$(1)="Le traitre!":R$(2)="Aurait-il ":R$(3)="deja decouvert":R$(4)="mes faiblesses?":R$(5)=V$:Z9=1
375 CLS#1:GOSUB 451:WA=100:GOSUB 3:GOTO 33
376 IF(C=6 OR C=7)AND CA<(FX/1.6)THEN 32
377 IF FNA(2)=1 THEN GOTO 32
378 R$(1)="...VOTRE ":R$(2)="SAVOIR-FAIRE":R$(3)="VOUS VAUT DES":R$(4)="FELECITATIONS!":R$(5)=V$
379 IF C=0 OR C=6 OR C=7 THEN R$(2)="DELICATESSE"
380 IF FNA(2)=1 THEN GOSUB 456:PRINT CHR$(7):WA=150:GOSUB 3:VA=10:GOSUB 73:GOTO 33
381 R$(1)="Un facheux":R$(2)="lumbago":R$(3)="vous a":R$(4)="terriglement":R$(5)="handicape."
382 GOSUB 55 GOSUB 456:GOSUB 339:WA=150:GOSUB 3:VA=-10:GOSUB 73:GOTO 33
383 LOCATE#1,14,9:PRINT#1,CHR$(246)+CHR$(247)+CHR$(247)+CHR$(250):LOCATE#1,15,10:PRINT#1,CHR$(248)+CHR$(248):RETURN
384 FOR I=1 TO 3:WA=50:GOSUB 3:LOCATE#1,14,9:PRINT#1," "+CHR$(247)+CHR$(247)+" ":SOUND 7,902,10,0,3,2:WA=20:GOSUB 3
385 GOSUB 383:SOUND 7,225,5,0,4,,1:NEXT I:RETURN
386 FOR I=200 TO 600 STEP 30:SOUND 1,I,30,14,,1:NEXT I:RETURN
387 R$(1)="MALCHANCE!":R$(2)="Vous avez":R$(3)="perdu votre":R$(4)="portefeuille.":R$(5)="En poche:30f":GOSUB 456
388 SOUND 7,2551,-10,0,3:WA=400:GOSUB 3:PRINT CHR$(7):FP=FR:FR=30:GOSUB 60:PP=4:PR=1:RETURN
389 IF FNA(8)<>4 THEN RETURN
390 R$(1)="Chance!":R$(2)="Vous avez":R$(3)="retrouve votre":R$(4)="portefeuille.":R$(5)=V$:FR=FP:FP=0
391 GOSUB 107:GOSUB 456:WA=250:GOSUB 3:GOSUB 60:PR=0:RETURN
392 R$(1)="CONTENT,":R$(2)="N'EST-CE PAS?":R$(3)="CA NE MARCHE":R$(4)="PAS MAL...":R$(5)=V$:BR=0
393 GOSUB 54:PRINT CHR$(7):GOSUB 456:WA=250:GOSUB 3:GOSUB 54
394 IF FNA(3)>1 AND CA<(FX/2.7)THEN 396
395 RETURN
396 R$(1)="..Vous lui ":R$(2)="communiquez":R$(3)="votre bonne":R$(4)="humeur...":R$(5)=V$:VA=15:GOSUB 456:PRINT CHR$(7):WA=120:GOSUB 3:GOSUB 73:GOSUB 55:CLS#1:RETURN
397 GOSUB 73:GOSUB 55:RETURN
398 LOCATE#CCC,FX,FY:FOR I=1 TO LEN(AF$):PRINT#CCC,MID$(AF$,I,1);:SOUND 7,884,2,0,4:FOR J=1 TO 500:NEXT J:NEXT I:RETURN
399 PRINT CHR$(7):WA=35:GOSUB 3:GOTO 285
400 PRINT CHR$(7):GOSUB 456:GOSUB 45
401 YA=3:CCC=1:CA$="...DESOLE!":GOSUB 402:CA$="VOTRE RIVAL":GOSUB 402:CA$="VOUS A FAUCHE":GOSUB 402:CA$="LA BELLE!!!":GOSUB 402:PG$="PERDUE!":CM$="Helas sans resultat!":GOSUB 272:SOUND 7,1012,-20,0,3:WA=200:GOSUB 3:PRINT CHR$(7):GOTO 314
402 XA=2:GOSUB 4:YA=YA+2:RETURN
403 CLS#1:YA=3:CCC=1:CA$="JE DEFAILLE,":GOSUB 402:CA$="JE CRAQUE!":GOSUB 402:PRINT CHR$(7):CA$="...TU M'AS":GOSUB 402:CA$="CONQUISE,":GOSUB 402:CA$=" DON JUAN!!!":GOSUB 402:O=4:V=10:GOSUB 386:PG$="GAGNEE!":CM$="     BRAVO!"
404 GOSUB 263:GOSUB 269:GOSUB 265
405 FOR I=1 TO 4:GOSUB 271:NEXT I:GOSUB 270:WA=10:GOSUB 3:GOSUB 314:GOSUB 285
406 IF RY=1 THEN W=0:GOTO 408
407 SOUND 7,159,200,0,4:FOR I=0 TO 6:GOSUB 410:WA=10:GOSUB 3:GOSUB 409:WA=15:GOSUB 3:NEXT I:W=400
408 GOSUB 409:RY=1:G=0:RI=1:RETURN
409 LOCATE#2,21,4:PRINT#2,"!Vous avez 1 RIVAL";:RETURN
410 LOCATE#2,21,4:PRINT#2,"                  ";:RETURN
411 IF NQ<5 THEN RETURN
412 GOSUB 262:CCC=1:CA$="...OOOHHH!":CLS#1:XA=2:YA=6:GOSUB 4:GOSUB 339:GOSUB 276:LOCATE XX-2,YY:PRINT V$
413 PEN#4,0:LOCATE#4,3,5:PRINT#4,"O":WA=50:GOSUB 3:LOCATE#4,3,6:PRINT#4,"O":WA=50:GOSUB 3:CA$=" ..ELLE PLEURE!":XA=2:YA=7:CLS#1:GOSUB 4:WA=100:GOSUB 3
414 RD=FNA(3)
415 CLS#1:R$(1)="Elle souffre":R$(2)="d'une rage":R$(3)="de dents.":R$(4)=V$
416 IF RD=3 THEN R$(1)="Elle a perdu":R$(2)="son emploi.":R$(3)=V$
417 IF RD=2 THEN 436
418 R$(5)=V$:GOSUB 107:GOSUB 456:WA=300:GOSUB 3:CLS#1
419 R$(1)="Entrez le mot":R$(2)="qui la soulagera":R$(3)="-il figure":R$(4)="parmi  les":R$(5)="mots-clefs."
420 IF RD>=2 THEN R$(2)="qui la deridera"
421 PRINT CHR$(7):GOSUB 456
422 GOSUB 45
423 QS=0:IF I$="MOTS"AND DM=0 THEN GOSUB 437
424 IF RD=1 AND I$="CALIN"THEN 434
425 IF RD=2 AND I$="BEBE"THEN 434
426 IF RD=3 AND(I$="MARIAGE"OR I$="VOYAGE")THEN 434
427 QS=QS+1:IF QS>=1 THEN 429
428 GOTO 422
429 VA=-20:R$(1)="DOMMAGE:":R$(2)="UN AUTRE":R$(3)="LA CONSOLERA."
430 R$(4)="On continue..":R$(5)=V$:QS=0:BR=0:RN=RN+1:GOSUB 54
431 GOSUB 432:GOSUB 107:GOSUB 456:WA=100:GOSUB 3:GOSUB 73:GOSUB 55:RETURN
432 PEN#4,CRAY(5):LOCATE#4,3,6:PRINT#4," ":WA=50:GOSUB 3:LOCATE#4,3,5:PRINT#4," ":RETURN
433 RETURN
434 VA=30:GOSUB 432:TU=0:R$(1)="Merci!":R$(2)="Cela va mieux":R$(3)="maintenant.":R$(4)=V$:R$(5)=V$:GOSUB 451
435 GOSUB 73:GOSUB 55:RETURN
436 R$(1)="Son petit chien":R$(2)="a ete mange":R$(3)="par plus gros":R$(4)="que lui.":TU=1:GOTO 418
437 AJ=300:IF AJ>FR THEN RETURN
438 FR=FR-AJ:QS=QS-1:FOR I=0 TO 55:LOCATE XX,YY:PRINT V$:LOCATE XX,YY:PRINT MT$(I):WA=50:GOSUB 3:NEXT I:PRINT CHR$(7);:GOSUB 60:RETURN
439 R$(1)="PAS SI VITE,":R$(2)="MON AMI:":R$(3)="je ne suis pas":R$(4)="celle que":R$(5)="vous croyer!":GOSUB 456:VA=-10
440 GOSUB 272:GOSUB 73:GOSUB 55:GOTO 13
441 R$(1)="Trois fois,":R$(2)="c'est trop:":R$(3)="...changez":R$(4)="de disque,":R$(5)="mon vieux,":IF TT=0 THEN 443
442 R$(1)=PR$+",":R$(2)="mon vieux,":R$(3)="change un peu":R$(4)="de disque.":R$(5)=V$:IF CA>(FX/1.2)THEN R$(2)="mon choux,"
443 GOSUB 451:RN=RN+1:RETURN
444 CLS#1:R$(1)="Au point ou":R$(2)="on en est,":R$(3)="on pourrait":R$(4)="se tutoyer,":R$(5)="non?!":GOSUB 55:GOSUB 451:WA=150:GOSUB 3
445 R$(1)="..et puis,":R$(2)="tu pourrais":R$(3)="aussi me dire":R$(4)="ton prenom?":R$(5)=V$:GOSUB 451:WA=50:GOSUB 3:PRINT CHR$(7)
446 LOCATE#1,2,9:PRINT#1,"TON PRENOM?":GOSUB 45:PR$=I$:IF LEN(PR$)>14 THEN PR$=LEFT$(PR$,14)
447 VA=(FNA(10)+5):GOSUB 73:GOSUB 268
448 GOSUB 269:RETURN
449 RR$="EPUISEE":GOSUB 457:GOTO 24
450 RR$="INSUFFIS.":GOSUB 457:GOSUB 60:GOTO 13
451 GOSUB 54:XP=17:YP=3:PRINT CHR$(7):LZ=5:IF R$(2)=V$THEN LZ=2
452 IF R$(3)=V$THEN LZ=3
453 IF R$(4)=V$THEN LZ=4
454 FOR J1=1 TO LZ:LG=LEN(R$(J1)):FOR I1=LG TO 1 STEP-1:LOCATE#1,XP,YP:PRINT#1,MID$(R$(J1),I1,1):XP=XP-1:NEXT I1
455 XP=17:YP=YP+1:NEXT J1:RETURN
456 CLS#1:FOR I=1 TO 5:LOCATE#1,3,4+I:PRINT#1,R$(I):NEXT I:RETURN
457 SOUND 7,225,0,4:FOR I=1 TO 5:GOSUB 458:WA=10:GOSUB 3:GOSUB 459:WA=15:GOSUB 3:NEXT I:WA=300:GOSUB 3:RETURN
458 LOCATE#2,30,3:PRINT#2,RR$;:RETURN
459 LOCATE#2,30,3:PRINT#2,"         ";:RETURN
460 IF CA>(FX/2)AND TT=0 THEN TT=1:GOTO 444
461 IF CA>(FX/2)THEN 488
462 IF CA<=(FX/3)THEN 504
463 IF CA>(FX/3)AND CA<=(FX/2)THEN 466
464 IF CA>(FX/2)AND CA<=(FX/1.2)THEN 471
465 IF CA>(RX/1.2)THEN 480
466 IF VA<26 THEN 488
467 R$(1)="Vous alors!":R$(2)="...Vous savez":R$(3)="vous y prendre":R$(4)="avec les femmes."
468 IF Z2=1 THEN R$(1)="Voila ce que":R$(2)="j'appelle":R$(3)="un homme.":R$(4)=V$:R$(5)=V$
469 IF Z2=2 THEN R$(1)="...Je vous ":R$(2)="ecoute":R$(3)="et ne m'en":R$(4)="lasse pas."
470 R$(5)=V$:Z2=Z2+1:GOTO 451
471 IF VA>34 THEN 477
472 R$(1)=PR$+",":R$(2)="TU ME COMBLES":R$(3)="DE JOIE.":R$(4)=V$:R$(5)=V$
473 IF Z8=1 THEN R$(1)="J'en suis ":R$(2)="ABSOLUMENT":R$(3)="ravie."
474 IF Z8=2 THEN R$(1)=PR$+",":R$(2)="TU ES UN AMOUR!":R$(3)=V$
475 Z8=Z8+1:IF Z8=3 THEN Z8=0
476 GOTO 451
477 IF Z6=1 THEN 479
478 R$(1)="ENCORE,":R$(2)=PR$+",":R$(3)="ENCORE!":R$(4)="JAMAIS JE NE":R$(5)="M'EN LASSERAI.":Z6=Z6+1:GOTO 451
479 R$(1)="JE CROIS,":R$(2)=PR$+",":R$(3)="JE CROIS QUE":R$(4)="...OH!":R$(5)="J'EN ROUGIS.":GOSUB 262:Z6=0:GOTO 451
480 IF VA>34 THEN 484
481 R$(1)=PR$+",":R$(2)="Tu es":R$(3)="un tresor."
482 IF Z3=1 THEN R$(1)="...UN REVE,":R$(2)="cet homme!":R$(3)=V$:R$(4)=V$:R$(5)=V$:Z3=Z3+1:IF Z3=2 THEN Z3=0
483 GOTO 451
484 R$(1)="SAIS-TU,":R$(2)=PR$+",":R$(3)="QUE TU COMMENCE":R$(4)="SERIEUSEMENT":R$(5)="A ME PLAIRE.."
485 IF FNA(2)=1 THEN R$(2)="mon chou,"ELSE R$(2)="mon biquet,"
486 IF FNA(2)=1 THEN 502 ELSE 503
487 GOTO 451
488 REM
489 R$(1)="J'en suis ":R$(2)="ravie..."
490 IF Z1=1 THEN R$(1)="Chic,alors!":R$(2)=V$
491 IF Z1=2 THEN R$(1)="...Sympa,":R$(2)="ce type."
492 IF Z1=3 THEN R$(1)="Interessant!":R$(2)=V$
493 R$(3)=V$:R$(4)=V$:R$(5)=V$:Z1=Z1+1
494 GOTO 451
495 IF CA<(FX/2.6)THEN RETURN
496 IF TT=1 AND TZ=2 THEN RETURN
497 IF TT=0 AND TZ=1 THEN RETURN
498 IF TT=1 THEN 500
499 R$(1)="Vous n'etes pas":R$(2)="mal non plus,":R$(3)="Savez-vous?":TZ=1:R$(4)=V$:R$(5)=V$:GOSUB 265:GOSUB 451:GOTO 501
500 R$(1)="SAIS TU,":R$(2)="MON PETIT":R$(3)=PR$+",":R$(4)="QUE TU ES AUSSI":R$(5)="TRES SEDUISANT!":GOSUB 265:GOSUB 451:PRINT CHR$(7):TZ=2
501 WA=350:GOSUB 3:RETURN
502 R$(1)=PR$+",":R$(3)="je ne sais":R$(4)="plus comment":R$(5)="te remercier.":R$(2)="mon chou,":GOTO 451
503 R$(1)=PR$+",":R$(2)="TU ES TOUT":R$(3)="SIMPLEMENT":R$(4)="DELICIEUX.":R$(5)=V$:GOTO 451
504 IF VA<30 THEN 510
505 R$(1)="Pas de doute,":R$(2)="il me cherche.":R$(3)=V$
506 IF Z5=1 THEN R$(1)="...Interessant!":R$(2)=V$:R$(3)=V$
507 IF Z5=2 THEN R$(1)="De plus en plus":R$(2)="interessant!":R$(3)=V$
508 R$(4)=V$:R$(5)=V$:Z5=Z5+1:IF Z5=3 THEN Z5=2
509 GOTO 451
510 ON Z7 GOTO 511,514,515,516,517
511 R$(1)="Interessant,":R$(2)="ce type.":R$(3)=V$:R$(4)=V$
512 IF C=0 OR C=1 OR C=2 OR C=8 OR C=10 THEN R$(2)="Cet homme."
513 GOTO 518
514 R$(1)="...Serait-il":R$(2)="intelligent?":R$(3)=V$:R$(4)=V$:GOTO 518
515 GOSUB 263:R$(1)="Il n'est pas mal":R$(2)="de sa personne,":R$(3)="de surcroit.":R$(4)=V$:GOTO 518
516 R$(1)="...Il commence":R$(2)="serieusement":R$(3)="a m'interesser.":R$(4)=V$:R$(5)=V$:GOTO 518
517 R$(1)="Tres interessant!":R$(2)=V$:R$(3)=V$:R$(4)=V$
518 R$(5)=V$:Z7=Z7+1:IF Z7=6 THEN Z7=5
519 GOTO 451
520 AZ=FNA(4)
521 R$(1)="..Diable!":R$(2)="VOTRE RIVAL "
522 IF AZ=1 THEN R$(3)="lui a offert":R$(4)="un bijou.":GOTO 526
523 IF AZ=2 THEN R$(3)="lui propose":R$(4)="un voyage.":GOTO 526
524 IF AZ=3 THEN 528
525 IF AZ=4 THEN R$(3)="lui a promis":R$(4)="des merveilles."
526 R$(5)="...Coup nul.":VA=0:GOSUB 278:GOSUB 456
527 SOUND 7,60,200,0,4:WA=400:GOSUB 3:RETURN
528 R$(3)="n'arrete pas":R$(4)="de lui dire":R$(5)="du mal de vous."
529 R$(1)="Le traitre!"
530 IF TT=0 THEN VA=-10 ELSE VA=-2
531 GOSUB 278:GOSUB 54:GOSUB 456:GOSUB 527:GOSUB 73:WA=200:GOSUB 3
532 GOSUB 55:RETURN
533 FOR J=1 TO F:FOR I=0 TO 10:READ CRAY(I):NEXT I:NEXT J
534 INK 0,0:ON F GOSUB 535,536,537,538,539:GOTO 540
535 INK 1,25:INK 2,6:INK 3,19:RETURN
536 INK 1,17:INK 2,16:INK 3,14:RETURN
537 INK 1,11:INK 2,16:INK 3,18:RETURN
538 INK 1,6:INK 2,18:INK 3,24:RETURN
539 INK 1,6:INK 2,24:INK 3,22:RETURN
540 PEN CRAY(0):CCC=0:XA=5:YA=1:CA$="DON JUAN":GOSUB 4:LOCATE 5,3:PRINT"********":LOCATE 4,5:PRINT"NIVEAU:";:PRINT NN
541 WINDOW#1,1,19,7,20:PAPER#1,CRAY(1):PEN#1,0:CLS#1:WINDOW#2,1,40,22,25:PAPER#2,CRAY(4):CLS#2:PEN#2,CRAY(10)
542 LOCATE#2,3,2:PRINT#2,"ENTREZ LE MOT-CLEF|COTE D'AMOUR: 0":LOCATE#2,3,3:PRINT#2,"QUI LA SEDUIRA ...|Fortune:";:PRINT#2,FR:LOCATE#2,3,4:PRINT#2,".Pour rompre:'!'  |Rival :Neant"
543 FF$=CHR$(254):WINDOW#3,21,40,1,20:PAPER#3,CRAY(6):CLS#3:PEN CRAY(9):FOR I=1 TO 10:LOCATE#3,1,I:PRINT#3,FF$+FF$:LOCATE#3,20,I:PRINT#3,FF$:LOCATE#3,1,10+I:PRINT#3,FF$:NEXT I:LOCATE#3,1,20:PRINT#3,STRING$(&14,FF$);
544 PEN#3,CRAY(5):FOR I=3 TO 5:LOCATE#3,10,I:PRINT#3,FF$+"  "+FF$:NEXT I:WINDOW#4,26,35,6,13:PAPER#4,CRAY(5):CLS#4
545 PEN#3,CRAY(9):LOCATE#3,4,15:GOSUB 550:LOCATE#3,4,18:GOSUB 550:FOR I=12 TO 13:LOCATE#3,5,I:PRINT#3,FF$+FF$:LOCATE#3,15,I:PRINT#3,FF$+FF$:NEXT I:FOR I=16 TO 17:LOCATE#3,4,I:PRINT#3,FF$+"  "+FF$
546 LOCATE#3,14,I:PRINT#3,FF$+"  "+FF$:PRINT#3,:NEXT I
547 PEN#3,CRAY(5):LOCATE#3,7,14:PRINT#3,STRING$(&8,FF$):FOR I=14 TO 19:LOCATE#3,8,I:PRINT#3,STRING$(&6,FF$):NEXT I
548 GOSUB 269:LOCATE#4,5,5:PRINT#4,"..":PEN#3,0:PRINT#3,CHR$(22)+CHR$(1):LOCATE#3,10,15:PRINT#3,CHR$(244)+CHR$(244):LOCATE#3,14,14:PRINT#3,"/":PRINT#3,CHR$(22)+CHR$(0)
549 PEN#4,CRAY(7):GOSUB 275:GOSUB 270:WA=80:GOSUB 3:GOSUB 262:RETURN
550 PRINT#3,FF$+FF$+FF$+FF$+SPACE$(6)+FF$+FF$+FF$+FF$;:RETURN
551 RETURN
552 C$=INKEY$:IF C$=""THEN 552 ELSE C$=UPPER$(C$):C=ASC(C$):PEN 0:PRINT C;"C":STOP
553 DATA 3,1,0,3,2,1,2,2,1,3,0,1,2,3,1,3,2,0,1,3,3,0,3,1,3,3,2,2,0,1,2,1,1,1,2,0,2,1,3,0,1,2,2,2,3,3,0,3,1,3,2,1,0,1,0
554 ENV 1,15,-1,6,1,0,30
555 ON SON GOTO 556,557
556 SOUND 7,1000,0,15,1:RETURN
557 SOUND 7,1,-4,8,1:RETURN
558 RETURN
559 FOR III=1 TO WA*10:NEXT III:RETURN
560 PRINT CHR$(7);:REM SON
561 RETURN
562 TT1=VPOS(#CCC):FOR I=1 TO LEN(CA$)
563 ZZI=ASC(MID$(CA$,I,1))
564 IF ZZI>64 AND ZZI<91 THEN ZI=65+ZZI:ZY=105+ZZI:GOTO 573
565 IF ZZI>47 AND ZZI<58 THEN ZI=161+ZZI:ZY=171+ZZI:GOTO 573
566 IF ZZI=32 THEN ZI=32:ZY=32:GOTO 573
567 IF ZZI=44 THEN ZI=32:ZY=238:GOTO 573
568 IF ZZI=63 THEN ZI=233:ZY=234:GOTO 573
569 IF ZZI=33 THEN ZI=231:ZY=232:GOTO 573
570 IF ZZI=46 THEN ZI=32:ZY=235:GOTO 573
571 IF ZZI=42 THEN ZI=236:ZY=237:GOTO 573
572 IF ZZI=39 THEN ZI=230:ZY=32 ELSE ZI=32:ZY=ZZI-32
573 LOCATE#CCC,XA,YA:PRINT#CCC,CHR$(ZI):LOCATE#CCC,XA,YA+1:PRINT#CCC,CHR$(ZY)
574 XA=XA+1:NEXT I:LOCATE 1,TT1+2:RETURN
